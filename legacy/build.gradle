/*
 *   Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 *   Licensed under the Apache License, Version 2.0 (the "License").
 *   You may not use this file except in compliance with the License.
 *   A copy of the License is located at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file. This file is distributed
 *   on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 *   express or implied. See the License for the specific language governing
 *   permissions and limitations under the License.
 */

plugins {
    id 'java-library'
    id "io.freefair.lombok" version "5.0.0-rc4"
    id 'antlr'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.3"
}

ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.TXT')
    noticeFile = rootProject.file('NOTICE')
}

// TODO: need to fix java doc to enable JavaDoc
javadoc.enabled = false

// Set source and target to be compatible with Java 8
// Commented them out as the default is java 8
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// TODO: fix compiler warnings
compileJava.options.warnings = false
compileJava {
    options.compilerArgs.addAll(["-processor", 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'])
    doFirst {
        // TODO: do not fail build on warnings, need to fix all compiler warnings
        options.compilerArgs.remove('-Werror')
        // TODO: need to fix all java doc format
        options.compilerArgs.remove('-Xdoclint:all')
    }
}

// TODO: Similarly, need to fix compiling errors in test source code
compileTestJava.options.warnings = false
compileTestJava {
    options.compilerArgs.addAll(["-processor", 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'])
    doFirst {
        options.compilerArgs.remove('-Werror')
        options.compilerArgs.remove('-Xdoclint:all')
    }
}

// TODO: Need to update integration test to use ElasticSearch test framework
test {
    include '**/*Test.class'
    exclude 'com/amazon/opendistroforelasticsearch/sql/intgtest/**'
    // Gradle runs unit test using a working directory other and project root
    // set 'project.root' property to allow unit test classes to access test resources
    systemProperty('project.root', project.projectDir.absolutePath)
}

generateGrammarSource {
    arguments += ['-visitor', '-package', 'com.amazon.opendistroforelasticsearch.sql.antlr.parser']
    source = sourceSets.main.antlr
    outputDirectory = file("build/generated-src/antlr/main/com/amazon/opendistroforelasticsearch/sql/antlr/parser")
}

// Remove ANTLR plugin jars as it's specified as 'compile' dependency internally
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

//****************************************************************************/
// Dependencies
//****************************************************************************/
dependencies {
    compileOnly "org.elasticsearch:elasticsearch:${es_version}"
    compile group: 'com.alibaba', name: 'druid', version:'1.0.15'
    compile group: 'org.locationtech.spatial4j', name: 'spatial4j', version:'0.7'
    compile group: "org.elasticsearch.plugin", name: 'parent-join-client', version: "${es_version}"
    compile group: "org.elasticsearch.plugin", name: 'reindex-client', version: "${es_version}"
    compile group: 'com.google.guava', name: 'guava', version:'15.0'
    compile group: 'org.json', name: 'json', version:'20180813'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    // ANTLR gradle plugin and runtime dependency
    antlr "org.antlr:antlr4:4.7.1"
    compile "org.antlr:antlr4-runtime:4.7.1"

    compileOnly group: 'javax.servlet', name: 'servlet-api', version:'2.5'

    testImplementation "org.elasticsearch.test:framework:${es_version}"
    testCompile group: "org.elasticsearch.client", name: 'transport', version: "${es_version}"

    // JDBC drivers for comparison test. Somehow Apache Derby throws security permission exception.
    testCompile group: 'com.amazon.opendistroforelasticsearch.client', name: 'opendistro-sql-jdbc', version: '1.3.0.0'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.200'
    testCompile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.28.0'
    //testCompile group: 'org.apache.derby', name: 'derby', version: '10.15.1.3'
}


