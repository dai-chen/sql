/*
 *   Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 *   Licensed under the Apache License, Version 2.0 (the "License").
 *   You may not use this file except in compliance with the License.
 *   A copy of the License is located at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file. This file is distributed
 *   on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 *   express or implied. See the License for the specific language governing
 *   permissions and limitations under the License.
 */

apply plugin: 'elasticsearch.build'
apply plugin: 'elasticsearch.rest-test'
apply plugin: 'java'
apply plugin: 'io.freefair.lombok'

ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.TXT')
    noticeFile = rootProject.file('NOTICE')
}

repositories {
    mavenCentral()
}

compileTestJava {
    options.compilerArgs.addAll(["-processor", 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'])
    doFirst {
        options.compilerArgs.remove('-Werror')
        options.compilerArgs.remove('-Xdoclint:all')
    }
}

dependencies {
    testCompile group: 'org.elasticsearch.test', name: 'framework', version: '7.6.1'
    testCompile group: 'org.elasticsearch.client', name: 'elasticsearch-rest-high-level-client', version: '7.6.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.1'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version:'2.11.1'
    testCompile group: 'org.json', name: 'json', version:'20180813'

    testCompile project(':legacy') // TODO: Should be decoupled with legacy module and move required classes to common module.

    //testImplementation "org.elasticsearch.test:framework:${es_version}"
    //testCompile group: "org.elasticsearch.client", name: 'transport', version: "${es_version}"

    // JDBC drivers for comparison test. Somehow Apache Derby throws security permission exception.
    testCompile group: 'com.amazon.opendistroforelasticsearch.client', name: 'opendistro-sql-jdbc', version: '1.3.0.0'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.200'
    testCompile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.28.0'
    //testCompile group: 'org.apache.derby', name: 'derby', version: '10.15.1.3'

}

dependencyLicenses.enabled = false
testingConventions.enabled = false
checkstyleTest.ignoreFailures = true
forbiddenApisTest.enabled = false

tasks.integTest.dependsOn(':plugin:bundlePlugin')
testClusters.integTest {
    testDistribution='oss'
    plugin file(tasks.getByPath(':plugin:bundlePlugin').archiveFile)
}

integTest.runner {
    systemProperty 'tests.security.manager', 'false'
    systemProperty('project.root', project.projectDir.absolutePath)

    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }

    // Run different task based on test type. "exclude" is required for each task.
    def testType = System.getProperty("testType")
    if (testType == 'doctest') { // Doctest to generate documentation
        include 'com/amazon/opendistroforelasticsearch/sql/doctest/**/*IT.class'
        exclude 'com/amazon/opendistroforelasticsearch/sql/correctness/**'
        exclude 'com/amazon/opendistroforelasticsearch/sql/esintgtest/**'

    } else if (testType == 'comparison') { // Comparision testing
        include 'com/amazon/opendistroforelasticsearch/sql/correctness/CorrectnessIT.class'
        exclude 'com/amazon/opendistroforelasticsearch/sql/doctest/**'
        exclude 'com/amazon/opendistroforelasticsearch/sql/esintgtest/**'

        // Enable logging output to console
        testLogging.showStandardStreams true

        // Pass down system properties to IT class
        systemProperty "esHost", System.getProperty("esHost")
        systemProperty "dbUrl", System.getProperty("dbUrl")
        systemProperty "otherDbUrls", System.getProperty("otherDbUrls")
        systemProperty "queries", System.getProperty("queries")

    } else { // Run all other integration tests. Skip doctest for now due to randomness in our stats API.
        include 'com/amazon/opendistroforelasticsearch/sql/esintgtest/**/*IT.class'
        exclude 'com/amazon/opendistroforelasticsearch/sql/doctest/**/*IT.class'
        exclude 'com/amazon/opendistroforelasticsearch/sql/correctness/**'
    }
}